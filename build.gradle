import java.time.Duration

/*
    adding the plugin jars to the classpath to apply them later.
    currently the new plugins DSL does apply them directly.
    there are other limitations too. See https://docs.gradle.org/current/userguide/plugins.html#sec:plugins_block
    we don't need to apply the jruby plugin on the rootProject.
*/
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        maven { url "https//oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.3.0"
    }
}

// modern plugins config
plugins {
  id "io.sdkman.vendors" version "2.0.0"
  id "signing"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
  id 'com.github.jruby-gradle.base' version '2.0.0'
  id 'codenarc'
}

// TIP use -PpublishRelease=true to active release behavior regardless of the version
status = project.hasProperty('publishRelease') && project.publishRelease.toBoolean() ?
    'release' : ((version == 'unspecified' || version.endsWith('-SNAPSHOT')) ? 'snapshot' : 'release')

// using ExpandoMetaClass to add isDistribution() Method to Project instances...
Project.metaClass.isDistribution = { delegate.getName().endsWith("-distribution") }

nexusPublishing {
  repositories {
    sonatype {
      if (project.hasProperty("sonatypeUsername")) {
        username = project.sonatypeUsername
      }
      if (project.hasProperty("sonatypePassword")) {
        password = project.sonatypePassword
      }
      repositoryDescription = "Release ${project.group} ${project.version}"
    }
  }
  clientTimeout = Duration.ofMinutes(5)
}


ext {
  buildDateTime = new Date()
  (buildDateOnly, buildTimeOnly) = new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss.SSSZ').format(buildDateTime).split(' ')
  statusIsRelease = (status == 'release')

  // jar versions
  arquillianVersion = '1.6.0.Final'
  arquillianSpockVersion = '1.0.0.CR1'
  asciidoctorjPdfVersion = '1.6.0'
  asciidoctorjEpub3Version = '1.5.1'
  asciidoctorjDiagramVersion = '2.1.2'
  asciidoctorjRevealJsVersion = '4.1.0'
  commonsioVersion = '2.4'
  guavaVersion = '18.0'
  hamcrestVersion = '1.3'
  jcommanderVersion = '1.72'
  jrubyVersion = '9.2.17.0'
  jsoupVersion = '1.12.1'
  junitVersion = '4.13.2'
  assertjVersion = '3.19.0'
  nettyVersion = '4.1.58.Final'
  saxonVersion = '9.9.0-2'
  xmlMatchersVersion = '1.0-RC1'
  pdfboxVersion = '1.8.16'

  // gem versions
  asciidoctorGemVersion = project.hasProperty('asciidoctorGemVersion') ? project.asciidoctorGemVersion : '2.0.15'
  coderayGemVersion = '1.1.3'
  rougeGemVersion = '3.26.0'

  codenarcVersion = '2.1.0'
  groovyVersion = '2.4.21'
  erubisGemVersion = '2.7.0'
  hamlGemVersion = '5.0.4'
  openUriCachedGemVersion = '0.0.5'
  slimGemVersion = '4.1.0'
  concurrentRubyGemVersion = '1.1.7'
  spockVersion = '1.3-groovy-2.4'
  threadSafeGemVersion = '0.3.6'
  tiltGemVersion = '2.0.10'
  osgiVersion = '7.0.0'
}

allprojects {
  group = 'org.asciidoctor'
  defaultTasks 'check'
}

subprojects {
  // NOTE applying Java plugin changes the status; take steps to preserve value
  def _status = status
  apply plugin: 'java-library'
  apply plugin: 'groovy'

  status = _status


  plugins.withType(JavaPlugin) {
    project.tasks.withType(JavaCompile) { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 8
      }
      if (project.hasProperty("showDeprecation")) {
        options.compilerArgs << "-Xlint:deprecation"
      }
      if (project.hasProperty("showUnchecked")) {
        options.compilerArgs << "-Xlint:unchecked"
      }
    }
    project.tasks.withType(GroovyCompile) { task ->
      if (JavaVersion.current().isJava11Compatible()) {
        task.options.release = 8
      }
    }
  }

  repositories {
    if (project.hasProperty('useMavenLocal') && project.useMavenLocal.toBoolean()) {
      mavenLocal()
    }
    mavenCentral()
  }

  apply plugin: 'codenarc'
  codenarc {
    configFile = rootProject.file('config/codenarc/codenarc.groovy')
  }

  if (!isIntegrationTestProject(it)) {

    dependencies {
      testImplementation "junit:junit:$junitVersion"
      testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
      testImplementation("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
      }
      testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
      testImplementation "org.jboss.arquillian.junit:arquillian-junit-container:$arquillianVersion"
      testImplementation "org.jboss.arquillian.spock:arquillian-spock-container:$arquillianSpockVersion"

      codenarc "org.codehaus.groovy:groovy:$groovyVersion"
      codenarc "org.codehaus.groovy:groovy-xml:$groovyVersion"
      codenarc "org.codehaus.groovy:groovy-ant:$groovyVersion"
      codenarc("org.codenarc:CodeNarc:$codenarcVersion") {
        exclude group: 'org.codehaus.groovy'
      }
    }
  }


  test {
    forkEvery = 10
    minHeapSize = '128m'
    maxHeapSize = '1024m'
    if (JavaVersion.current().isJava8Compatible()) {
      jvmArgs '-XX:-UseGCOverheadLimit'
    }

    testLogging {
      // events 'passed', 'failed', 'skipped', 'standard_out', 'standard_error'
      // events 'standard_out', 'standard_error'
      afterSuite { desc, result ->
        if (!desc.parent && logger.infoEnabled) {
          logger.info "Test results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        }
      }
    }
  }

}

configure(subprojects.findAll { !it.isDistribution() && it.name != 'asciidoctorj-api' && it.name != 'asciidoctorj-documentation' && it.name != 'asciidoctorj-test-support' && it.name != 'asciidoctorj-arquillian-extension' && !isIntegrationTestProject(it) }) {
  apply from: rootProject.file('gradle/versioncheck.gradle')
}

boolean isIntegrationTestProject(def project) {
  project.name in ['asciidoctorj-wildfly-integration-test',
                   'asciidoctorj-springboot-integration-test',
                   'springboot-app']
}

// apply JRuby and sources/javadocs packaging stuff for all subprojects except the distribution
configure(subprojects.findAll { !it.isDistribution() }) {

  apply plugin: 'com.github.jruby-gradle.base'
  apply from: rootProject.file('gradle/eclipse.gradle')
  apply plugin: 'idea'

  jruby {
    defaultRepositories false
  }

  repositories {
    ruby.gems()
  }

  javadoc {
    // Oracle JDK8 likes to fail the build over spoiled HTML
    options.addStringOption('Xdoclint:none', '-quiet')
    options.source('8')
  }
}

configure(subprojects.findAll { !it.name.endsWith('-distribution') && ! it.name.endsWith('-documentation')}) {

  java {
    withJavadocJar()
    withSourcesJar()
  }

  jruby {
    defaultRepositories = false
    defaultVersion = jrubyVersion
    // TODO I'd like to be able to customize the name of the gemInstallDir
  }

  ext {
    // path to use for the prepared jruby gems
    preparedGems = new File("$buildDir/preparedGems")
  }

  sourceSets {
    main {
      //let's register an output folder on the main SourceSet:
      output.dir(preparedGems, builtBy: 'jrubyPrepare')
      //it is now a part of the 'main' classpath and will be a part of the jar
    }
  }

  // QUESTION is this the right place to insert this task dependency in the lifecycle?
  // IMPORTANT The TMP or TEMP environment variable must be set for the gem install command to work on Windows
  processResources.dependsOn jrubyPrepare
}
