apply plugin: 'application'

if (project.statusIsRelease) {
    apply from: rootProject.file('gradle/sdkman.gradle')
}

dependencies {
  compile project(':asciidoctorj')
  compile project(':asciidoctorj-diagram')
  compile project(':asciidoctorj-epub3')
  compile project(':asciidoctorj-pdf')
}

jar.enabled = false

if ( !project.hasProperty('skip.signing') ) {
  signPom.enabled = false
}

configurations.all {
  artifacts.clear()
}

tasks.withType(AbstractArchiveTask) {
  baseName rootProject.name
  classifier 'bin' // use classifier for identification in repository list
  eachFile { it.path = it.path.replaceFirst('-bin(?=/)', '') }
  //destinationDir = ...
}

startScripts {
  // QUESTION should we make the application name asciidoctor instead of asciidoctorj?
  applicationName = rootProject.name
  mainClassName = 'org.asciidoctor.cli.AsciidoctorInvoker'
  defaultJvmOpts = [
    '-client', '-Xmn128m', '-Xms256m', '-Xmx256m',
    '-Xverify:none', '-XX:+UseFastAccessorMethods', '-XX:+TieredCompilation', '-XX:TieredStopAtLevel=1', '-XX:+DisableExplicitGC',
    '-Djruby.compile.mode=OFF', '-Djruby.compat.version=RUBY2_0'
  ]
}

artifacts {
  archives distZip
}
