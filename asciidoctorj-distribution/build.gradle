apply plugin: 'application'

dependencies {
  compile project(':asciidoctorj')
  compile project(':asciidoctorj-epub3')
  compile project(':asciidoctorj-diagram')

  compile("org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion") {
    transitive = false
  }
  testCompile project(':asciidoctorj-arquillian-extension')
  testCompile "org.apache.pdfbox:pdfbox:$pdfboxVersion"
}

jar.enabled = false
if ( !project.hasProperty('skip.signing') ) {
  signPom.enabled = false
}

configurations.all {
  artifacts.clear()
}

tasks.withType(AbstractArchiveTask) {
  baseName rootProject.name
  classifier 'bin' // use classifier for identification in repository list
  eachFile { it.path = it.path.replaceFirst('-bin(?=/)', '') }
  //destinationDir = ...
}

startScripts {
  // QUESTION should we make the application name asciidoctor instead of asciidoctorj?
  applicationName = rootProject.name
  mainClassName = 'org.asciidoctor.cli.AsciidoctorInvoker'
  defaultJvmOpts = [
    '-client', '-Xmn128m', '-Xms256m', '-Xmx256m',
    '-Xverify:none', '-XX:+UseFastAccessorMethods', '-XX:+TieredCompilation', '-XX:TieredStopAtLevel=1', '-XX:+DisableExplicitGC'
  ]
}

artifacts {
  archives distZip
}
