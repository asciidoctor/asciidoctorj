dependencies {
  compile project(':asciidoctorj')

  testCompile project(path: ':asciidoctorj-arquillian-extension')

  gems("rubygems:asciidoctor-pdf:$asciidoctorPdfGemVersion") {
    // Exclude gems provided by AsciidoctorJ core
    exclude module: 'asciidoctor'
    exclude module: 'thread_safe'
    // We must lock Prawn to 1.3.0 until AsciidoctorJ upgrades to JRuby 9.0.0.0
    exclude module: 'prawn'
  }
  gems "rubygems:prawn:$prawnGemVersion"
  gems "rubygems:rouge:$rougeGemVersion"

  testCompile "org.apache.pdfbox:pdfbox:$pdfboxVersion"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/*/data/fonts/*"
  include "gems/asciidoctor-pdf-${asciidoctorPdfGemVersion}/data/**"
  // Accomodate Prawn's non-conforming packaging
  include "gems/prawn-$prawnGemVersion/VERSION"
  include "gems/prawn-$prawnGemVersion/data/*.txt"
  include "gems/prawn-$prawnGemVersion/data/encodings/*"
  // Include Font Awesome fonts
  include "gems/prawn-icon-*/fonts/fa/*"
  // Accomodate Addressable's non-conforming packaging
  include "gems/addressable-*/data/*.data"

  exclude 'gems/rouge-*/lib/rouge/demos/**'
}

jrubyPrepareGems << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    eachFile {
      // convince Asciidoctor PDF to use the thread_safe 0.3.4 provided by AsciidoctorJ
      if (it.path =~ 'specifications/asciidoctor-pdf-1.5.0.*.gemspec') {
        it.filter { line ->
          line.replaceAll(/<thread_safe>, \["= 0.3.5"\]/, '<thread_safe>, ["= 0.3.4"]')
        }
      }
    }
    into sourceSets.main.output.resourcesDir
  }
}
