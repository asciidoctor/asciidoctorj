import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '3.0.4'
	id 'java'
}

group = 'org.asciidoctor.it'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	implementation project(':asciidoctorj')

	implementation platform(SpringBootPlugin.BOM_COORDINATES)
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(getToolchainVersion()))
	}
}

def getToolchainVersion() {
	if (DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
		if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_19))
			return 19
	}
	return 17
}

bootJar {
	requiresUnpack '**/asciidoctorj-*.jar'
}
