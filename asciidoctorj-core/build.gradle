apply plugin: 'osgi'

dependencies {
  compile "org.jruby:jruby-complete:$jrubyVersion"
  compile "com.beust:jcommander:$jcommanderVersion"
  gems "rubygems:asciidoctor:$asciidoctorGemVersion"
  gems "rubygems:coderay:$coderayGemVersion"
  gems "rubygems:erubis:$erubisGemVersion"
  gems "rubygems:haml:$hamlGemVersion"
  gems "rubygems:open-uri-cached:$openUriCachedGemVersion"
  gems "rubygems:slim:$slimGemVersion"
  gems "rubygems:thread_safe:$threadSafeGemVersion"
  gems "rubygems:tilt:$tiltGemVersion"
  // TODO could use dependency replacement feature to fix version of Saxon-HE
  testCompile("org.xmlmatchers:xml-matchers:$xmlMatchersVersion") { exclude module: 'Saxon-HE' }
  testCompile "net.sf.saxon:Saxon-HE:$saxonVersion"
  testCompile "com.google.guava:guava:$guavaVersion"
  testCompile "org.jsoup:jsoup:$jsoupVersion"
  testCompile "io.netty:netty-all:$nettyVersion"
  testCompile project(path: ':asciidoctorj-test-support', configuration: 'tests')

  testCompile "org.jboss.arquillian.container:arquillian-container-spi:$arquillianVersion"
  testCompile "org.jboss.arquillian.container:arquillian-container-test-spi:$arquillianVersion"
  testCompile "org.jboss.arquillian.container:arquillian-container-impl-base:$arquillianVersion"
  testCompile "org.jboss.arquillian.container:arquillian-container-test-impl-base:$arquillianVersion"
  testCompile "org.jboss.arquillian.junit:arquillian-junit-container:$arquillianVersion"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/asciidoctor-${asciidoctorGemVersion}/data/**"
}

jrubyPrepareGems << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    into sourceSets.main.output.resourcesDir
  }
}

javadoc {
  classpath = sourceSets.main.output + sourceSets.main.compileClasspath + project(':asciidoctorj-test-support').sourceSets.test.output
}

//jruby {
//	execVersion = '1.7.20'
//}

jar {
  manifest {
    symbolicName = 'org.asciidoctor'
    instruction 'Export-Package',
      "org.asciidoctor;version=\"${version}\"",
      "org.asciidoctor.ast;version=\"${version}\"",
      "org.asciidoctor.converter;version=\"${version}\"",
      "org.asciidoctor.converter.spi;version=\"${version}\"",
      "org.asciidoctor.extension;version=\"${version}\"",
      "org.asciidoctor.extension.spi;version=\"${version}\""
    instruction 'Import-Package',
      'com.beust.jcommander;resolution:=optional',
      '*'
  }
}
